<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
    <meta name="theme-color" content="#000000">
    <link rel="manifest" href="%PUBLIC_URL%/manifest.json">
    <link rel="shortcut icon" href="%PUBLIC_URL%/favicon.png">
    <title>GLTD</title>
  </head>
  <body>
    <noscript>
      Yo. What. You need to enable Javascript.
    </noscript>
    <div id="root"></div>
    <!-- This is the 'compute shader' for the water heightmap: -->
    <script id="heightmapFragmentShader" type="x-shader/x-fragment">
      #include <common>

      uniform vec2 mousePos;
      uniform float mouseSize;
      uniform float viscosityConstant;

      #define deltaTime ( 1.0 / 60.0 )
      #define GRAVITY_CONSTANT ( resolution.x * deltaTime * 3.0 )

      void main() {

          vec2 cellSize = 1.0 / resolution.xy;

          vec2 uv = gl_FragCoord.xy * cellSize;

          // heightmapValue.x == height
          // heightmapValue.y == velocity
          // heightmapValue.z, heightmapValue.w not used
          vec4 heightmapValue = texture2D( heightmap, uv );

          // Get neighbours
          vec4 north = texture2D( heightmap, uv + vec2( 0.0, cellSize.y ) );
          vec4 south = texture2D( heightmap, uv + vec2( 0.0, - cellSize.y ) );
          vec4 east = texture2D( heightmap, uv + vec2( cellSize.x, 0.0 ) );
          vec4 west = texture2D( heightmap, uv + vec2( - cellSize.x, 0.0 ) );

          float sump = north.x + south.x + east.x + west.x - 4.0 * heightmapValue.x;

          float accel = sump * GRAVITY_CONSTANT;

          // Dynamics
          heightmapValue.y += accel;
          heightmapValue.x += heightmapValue.y * deltaTime;

          // Viscosity
          heightmapValue.x += sump * viscosityConstant;

          // Mouse influence
          float mousePhase = clamp( length( ( uv - vec2( 0.5 ) ) * BOUNDS - vec2( mousePos.x, - mousePos.y ) ) * PI / mouseSize, 0.0, PI );
          heightmapValue.x += cos( mousePhase ) + 1.0;

          gl_FragColor = heightmapValue;
      }
  </script>

<!-- This is just a smoothing 'compute shader' for using manually: -->
<script id="smoothFragmentShader" type="x-shader/x-fragment">

      uniform sampler2D texture;

      void main() {

          vec2 cellSize = 1.0 / resolution.xy;

          vec2 uv = gl_FragCoord.xy * cellSize;

          // Computes the mean of texel and 4 neighbours
          vec4 textureValue = texture2D( texture, uv );
          textureValue += texture2D( texture, uv + vec2( 0.0, cellSize.y ) );
          textureValue += texture2D( texture, uv + vec2( 0.0, - cellSize.y ) );
          textureValue += texture2D( texture, uv + vec2( cellSize.x, 0.0 ) );
          textureValue += texture2D( texture, uv + vec2( - cellSize.x, 0.0 ) );

          textureValue /= 5.0;

          gl_FragColor = textureValue;

      }

  </script>

<!-- This is the water visualization shader, copied from the MeshPhongMaterial and modified: -->
<script id="waterVertexShader" type="x-shader/x-vertex">

      uniform sampler2D heightmap;

      #define PHONG

      varying vec3 vViewPosition;

      #ifndef FLAT_SHADED

          varying vec3 vNormal;

      #endif

      #include <common>
      #include <uv_pars_vertex>
      #include <uv2_pars_vertex>
      #include <displacementmap_pars_vertex>
      #include <envmap_pars_vertex>
      #include <color_pars_vertex>
      #include <morphtarget_pars_vertex>
      #include <skinning_pars_vertex>
      #include <shadowmap_pars_vertex>
      #include <logdepthbuf_pars_vertex>
      #include <clipping_planes_pars_vertex>

      void main() {

          vec2 cellSize = vec2( 1.0 / WIDTH, 1.0 / WIDTH );

          #include <uv_vertex>
          #include <uv2_vertex>
          #include <color_vertex>

          // # include <beginnormal_vertex>
          // Compute normal from heightmap
          vec3 objectNormal = vec3(
              ( texture2D( heightmap, uv + vec2( - cellSize.x, 0 ) ).x - texture2D( heightmap, uv + vec2( cellSize.x, 0 ) ).x ) * WIDTH / BOUNDS,
              ( texture2D( heightmap, uv + vec2( 0, - cellSize.y ) ).x - texture2D( heightmap, uv + vec2( 0, cellSize.y ) ).x ) * WIDTH / BOUNDS,
              1.0 );
          //<beginnormal_vertex>

          #include <morphnormal_vertex>
          #include <skinbase_vertex>
          #include <skinnormal_vertex>
          #include <defaultnormal_vertex>

      #ifndef FLAT_SHADED // Normal computed with derivatives when FLAT_SHADED

          vNormal = normalize( transformedNormal );

      #endif

          //# include <begin_vertex>
          float heightValue = texture2D( heightmap, uv ).x;
          vec3 transformed = vec3( position.x, position.y, heightValue );
          //<begin_vertex>

          #include <morphtarget_vertex>
          #include <skinning_vertex>
          #include <displacementmap_vertex>
          #include <project_vertex>
          #include <logdepthbuf_vertex>
          #include <clipping_planes_vertex>

          vViewPosition = - mvPosition.xyz;

          #include <worldpos_vertex>
          #include <envmap_vertex>
          #include <shadowmap_vertex>
      }
  </script>

<script id="terrainFragmentShaderNoise" type="x-shader/x-vertex">

    uniform float time;
    varying vec2 vUv;

    vec4 permute( vec4 x ) {

      return mod( ( ( x * 34.0 ) + 1.0 ) * x, 289.0 );

    }

    vec4 taylorInvSqrt( vec4 r ) {

      return 1.79284291400159 - 0.85373472095314 * r;

    }

    float snoise( vec3 v ) {

      const vec2 C = vec2( 1.0 / 6.0, 1.0 / 3.0 );
      const vec4 D = vec4( 0.0, 0.5, 1.0, 2.0 );

      // First corner

      vec3 i  = floor( v + dot( v, C.yyy ) );
      vec3 x0 = v - i + dot( i, C.xxx );

      // Other corners

      vec3 g = step( x0.yzx, x0.xyz );
      vec3 l = 1.0 - g;
      vec3 i1 = min( g.xyz, l.zxy );
      vec3 i2 = max( g.xyz, l.zxy );

      vec3 x1 = x0 - i1 + 1.0 * C.xxx;
      vec3 x2 = x0 - i2 + 2.0 * C.xxx;
      vec3 x3 = x0 - 1. + 3.0 * C.xxx;

      // Permutations

      i = mod( i, 289.0 );
      vec4 p = permute( permute( permute(
           i.z + vec4( 0.0, i1.z, i2.z, 1.0 ) )
           + i.y + vec4( 0.0, i1.y, i2.y, 1.0 ) )
           + i.x + vec4( 0.0, i1.x, i2.x, 1.0 ) );

      // Gradients
      // ( N*N points uniformly over a square, mapped onto an octahedron.)

      float n_ = 1.0 / 7.0; // N=7

      vec3 ns = n_ * D.wyz - D.xzx;

      vec4 j = p - 49.0 * floor( p * ns.z *ns.z );  //  mod(p,N*N)

      vec4 x_ = floor( j * ns.z );
      vec4 y_ = floor( j - 7.0 * x_ );    // mod(j,N)

      vec4 x = x_ *ns.x + ns.yyyy;
      vec4 y = y_ *ns.x + ns.yyyy;
      vec4 h = 1.0 - abs( x ) - abs( y );

      vec4 b0 = vec4( x.xy, y.xy );
      vec4 b1 = vec4( x.zw, y.zw );


      vec4 s0 = floor( b0 ) * 2.0 + 1.0;
      vec4 s1 = floor( b1 ) * 2.0 + 1.0;
      vec4 sh = -step( h, vec4( 0.0 ) );

      vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
      vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

      vec3 p0 = vec3( a0.xy, h.x );
      vec3 p1 = vec3( a0.zw, h.y );
      vec3 p2 = vec3( a1.xy, h.z );
      vec3 p3 = vec3( a1.zw, h.w );

      // Normalise gradients

      vec4 norm = taylorInvSqrt( vec4( dot( p0, p0 ), dot( p1, p1 ), dot( p2, p2 ), dot( p3, p3 ) ) );
      p0 *= norm.x;
      p1 *= norm.y;
      p2 *= norm.z;
      p3 *= norm.w;

      // Mix final noise value

      vec4 m = max( 0.6 - vec4( dot( x0, x0 ), dot( x1, x1 ), dot( x2, x2 ), dot( x3, x3 ) ), 0.0 );
      m = m * m;
      return 42.0 * dot( m*m, vec4( dot( p0, x0 ), dot( p1, x1 ),
                      dot( p2, x2 ), dot( p3, x3 ) ) );

    }

    float surface3( vec3 coord ) {

      float n = 0.0;

      n += 1.0 * abs( snoise( coord ) );
      n += 0.5 * abs( snoise( coord * 2.0 ) );
      n += 0.25 * abs( snoise( coord * 4.0 ) );
      n += 0.125 * abs( snoise( coord * 8.0 ) );

      return n;

    }

    void main( void ) {

      vec3 coord = vec3( vUv, -time );
      float n = surface3( coord );

      gl_FragColor = vec4( vec3( n, n, n ), 1.0 );

    }
  </script>

  <script id="terrainVertexShader" type="x-shader/x-vertex">
    varying vec2 vUv;
    uniform vec2 scale;
    uniform vec2 offset;

    void main( void ) {

      vUv = uv * scale + offset;
      gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

    }

  </script>

  </body>
</html>
